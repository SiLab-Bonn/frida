*******************************************************************************
* (c) Caeleste
* TSMC 65/55 stdcell parameter file
*
* document history
* 202405 Ahmed creation
* 20240620 Gaozhan add std_nmos_lowVth, PPD, TG

****************************************************************
* all technology dependent parameters are included from following file 
.include "v2.0_common.sp"
****************************************************************

*******************************************************
* label used by Nick to ensure validity of the file
.model STDCELL_VERSION_1_0 r
*******************************************************

**********************************
* STANDARD (vdd/vss) MOSFET models
**********************************

.subckt std_nmos D G S B W_=0 L_=0 M_=1
	.if (log_only)
		* In case all 3.3V transistors layouts are implanted as 1.2V transistors (cheap process)
		Xlog_nmos D G S B nch_mac W=W_ L=L_ M=M_
	.else
		Xstd_nmos D G S B nch_25_mac W=W_ L=L_ M=M_
	.endif
	.if (rh)
		Cp1 D G 'std_Crhgd*M_'
		Cp2 G S 'std_Crhgs*M_'
	.endif
.ends
	
.subckt std_pmos D G S B W_=0 L_=0 M_=1
	.if (log_only)
		* In case all 3.3V transistors layouts are implanted as 1.2V transistors
		Xlog_pmos D G S B pch_mac W=W_ L=L_ M=M_
	.else
		Xstd_pmos D G S B pch_25_mac W=W_ L=L_ M=M_ 
	.endif
.ends

*20240620 Gaozhan There is no lowVth for TSMC. nch_25 is used here.
*low Vth nMOSFET (no radhard variant yet)
.subckt std_nmos_lowVth D G S B W_=0 L_=0 M_=1
	Xstd_nmos_lowVth D G S B nch_25_mac W=W_ L=L_ M=M_
.ends

**********************************
* LOGIC (vcc/vee) MOSFET models 
**********************************

.subckt log_nmos D G S B W_=0 L_=0 M_=1
	Xlog_nmos D G S B nch_mac W=W_ L=L_ M=M_
	.if (rh)
		Cp1 D G 'log_Crhgd*M_'
		Cp2 G S 'log_Crhgs*M_'
	.endif
.ends

.subckt log_nmos_lowVth D G S B W_=0 L_=0 M_=1
	Xlog_nmos_lowVth D G S B nch_lvt_mac W=W_ L=L_ M=M_
.ends

.subckt log_pmos D G S B W_=0 L_=0 M_=1
	Xlog_pmos D G S B pch_mac W=W_ L=L_ M=M_
.ends

.subckt log_pmos_lowVth D G S B W_=0 L_=0 M_=1
	Xlog_pmos_lowVth D G S B pch_lvt_mac W=W_ L=L_ M=M_
.ends

**********************************
* RESISTOR models
**********************************
* by default we use N-doped poly resistor without silicide
.subckt std_rpoly a b W_=0 L_=0  M_=1
	Xrstd_poly a b rnpolywo L=L_ W=W_  M=M_
.ends

* NWELL diffusion resistor under active(OD)
.subckt std_rnwell a b W_=0 L_=0 M_=1
	Xrstd_nwell a b rnwod L=L_ W=W_ M=M_
.ends

**********************************
* Pixel models
**********************************
* qualitative TG model // OBSOLETE, use the device TG spice view
.subckt std_TG PPD TG FD SUB W_=2u L_=0.6u Vdep=0.7 Ctgfd=0.1f
	*0.7 is the Vth, 0.026 is the thermal voltage=kT/q, 4e-14 is the diode saturation current
	vdep  dep  0  Vdep
	gt_gate        FD     PPD  
		+ cur='v(FD,PPD) * min(max((-1e-5)*(v(PPD)-v(dep)),0), (4e-14)*exp((v(TG,PPD)-0.7)/0.026))'
	captgfd TG FD Ctgfd 
	*typical values in the order of 0.1 to 1 fF
.ends

* qualitative PPD  // OBSOLETE, use the device PPD spice view
* Vdep is (our approach) property of the TG not of the PPD 
* this capacitance is just representing the Vpinning drop versus amount of charge contained.
.subckt PPD PPD SUB W_=5u L_=5u
	cppd SUB PPD 'W_*L_*0.3e-3' 
	* the last factor calibrate it versus measurements
.ends


***************
* MISC models  // probably these are redundant as present in the schematic devices library
***************
.model Rshort r
.model Cparasitic c